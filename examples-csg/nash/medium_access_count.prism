// CSG model of medium access control 
// extends model of 
// Brenguier, R.: PRALINE: A tool for computing Nash equilibria in concurrent games. 
// In: Proc. CAV’13. LNCS, vol. 8044, pp. 890–895. Springer (2013)
// with probability of failure and energy bound

// concurrent stochastic game
csg

player p1
	[w1], [t1] // wait, transmit
endplayer

player p2
	[w2], [t2] // wait, transmit
endplayer

const int smax; // max number messages sent
const int emax; // energy bound
const int k; // time bound
const double q1; // probability single user successfully sends 
const double q2; // probability two users successfully send 

// uncomment for adversary generation
// label "adv" = true;

smodule medium_access
	
	sent1 : [0..smax]; // number of messages sent by player 1
	sent2 : [0..smax]; // number of messages sent by player 2
	
	e1 : [0..emax] init emax; // energy level of player 1
	e2 : [0..emax] init emax; // energy level of player 2
	
	// both player wait
	[w1,w2] true -> true;
	// player 1 tries to send only
	[t1,w2] e1>0 -> q1 : (sent1'=min(smax,sent1+1)) & (e1'=e1-1) + (1-q1) : (e1'=e1-1);
	// player 2 tries to send only
	[t2,w1] e2>0 -> q1 : (sent2'=min(smax,sent2+1)) & (e2'=e2-1) + (1-q1) : (e2'=e2-1);
	// both players try and send
	[t1,t2] e1>0 & e2>0 -> q2 : (sent1'=min(smax,sent1+1)) & (sent2'=min(smax,sent2+1)) & (e1'=e1-1) & (e2'=e2-1)
	                 + (1-q2) : (e1'=e1-1) & (e2'=e2-1);

endsmodule
