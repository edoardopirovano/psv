// CSG model of medium access control 
// extends model of 
// Brenguier, R.: PRALINE: A tool for computing Nash equilibria in concurrent games. 
// In: Proc. CAV’13. LNCS, vol. 8044, pp. 890–895. Springer (2013)
// with probability of failure when both players transmit

// concurrent stochastic game
csg 

player p1
	[w1], [t1] // wait, transmit
endplayer

player p2
	[w2], [t2] // wait, transmit
endplayer

const int emax; // energy bound
const int k; // time bound
const double q2; // probability two users successfully send 

// uncomment for adversary generation
// label "adv" = true;

smodule medium_access
	
	send : [0..3]; // used for rewards
	// 1 - first player successfully sends
	// 2 - second player successfully sends
	// 3 - both player successfully send
	
	e1 : [0..emax] init emax; // energy level of player 1
	e2 : [0..emax] init emax; // energy level of player 2
	
	// both player wait
	[w1,w2] true -> (send'=0);
	// player 1 tries to send only
	[t1,w2] e1>0 -> (send'=1) & (e1'=e1-1);
	// player 2 tries to send only
	[t2,w1] e2>0 -> (send'=2) & (e2'=e2-1);
	// both players try and send
	[t1,t2] e1>0 & e2>0 -> q2 : (send'=3) & (e1'=e1-1) & (e2'=e2-1)
	                 + (1-q2) : (send'=0) & (e1'=e1-1) & (e2'=e2-1);

endsmodule

// reward structure for player 1
srewards "r1"
	send=1 | send=3 : 1;
endsrewards

// reward structure for player 2
srewards "r2"
	send=2 | send=3 : 1;
endsrewards